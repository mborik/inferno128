		device	zxspectrum128

		include "pg0.inc"
		include "pg1.inc"
		include "pg3.inc"
		include "pg4.inc"
		include "pg6.inc"
		include "pg7.inc"

@page128	equ	#7FFD
@act128page	equ	#5B5C
@actborder	equ	#5C48
@FRAMES		equ	#5C78
@RUNZONE	equ	#8400
@basesp		equ	RUNZONE


		display "Composing kernel"

		org	#8000
		output	"final.bin"

@xchg.vram:	ld	a,#1D
		xor	#0A
		ld	(xchg.vram+1),a
@xchg.bnk:	ld	(act128page),a
		ld	bc,page128
		out	(c),a
		ret

;;-----------------------------------------------------------------------------

@kernel:	di
		ld	(backsp+1),sp
		ld	sp,basesp
; clear vram
		ld	hl,#4000
		ld	de,#4001
		ld	bc,#1AFF
		ld	(hl),l
		ldir
		ld	a,#10
		call	xchg.bnk
		di
		im	1
		ld	iy,#5C3A
		ld	hl,#2758
backsp		ld	sp,0
		exx
		ei
		ret


		display /D,(RUNZONE-$)," bytes free for stack"
		block	(RUNZONE-$),0

		outend

		export page128
		export act128page
		export actborder
		export FRAMES
		export basesp
		export kernel
		export RUNZONE
		export xchg.vram
		export xchg.bnk
